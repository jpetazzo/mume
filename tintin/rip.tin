#nop This script is written for v1.99.9 and higher

#list gains clear;

#action {^Never forget! Try to role-play...$} {info XPCOUNTER INIT, XP = %x, TNL = %X}
#action {^Reconnecting.$} {info XPCOUNTER INIT, XP = %x, TNL = %X}
#action {^XPCOUNTER INIT, XP = %d, TNL = %d} {#line gag;#variable xpcounter_total %1;#variable xpcounter_session %1}

#action {^%1 is dead! R.I.P.$} {xpcounter_killed {%1}}
#action {^%1 disappears into nothing.$} {xpcounter_killed {%1}}
#alias {xpcounter_killed} {#variable xpcounter_lastmob {%1};info XPCOUNTER UPDATE, XP = %x, TNL = %X, SESSION = %L}
#action {^XPCOUNTER UPDATE, XP = %d, TNL = %d, SESSION = %d}
{
	#line gag;
	#math {xpcounter_lastgain} {%1 - $xpcounter_total};
	#math {xpcounter_rate} {(%1 - $xpcounter_session) / %3};
        #echo {$xpcounter_lastgain XP gained for $xpcounter_lastmob. Current rate: $xpcounter_rate XP/min. TNL: %2.};
        #math {gains[$xpcounter_lastmob][exp]} {$gains[$mob][exp] + $xpcounter_lastgain};
        #math {gains[$xpcounter_lastmob][cnt]} {$gains[$mob][cnt] + 1};
	#variable {xpcounter_total} {%1}
}

#alias {rip}
{
        #showme {      Average Xp       Total Xp     #   Death List, R.I.P.};
        #showme {----------------------------------------------------------};
        
        #unvar {gains[final]};
        
        #foreach {$gains[]} {mob}
        {
                #echo {  %+14G %+14G %+5G   %s}                   
                        {$gains[$mob][exp] / $gains[$mob][cnt]}                 
                        {$gains[$mob][exp]}
                        {$gains[$mob][cnt]}
                        {$mob};            
                               
                #math gains[final][exp] $gains[final][exp] + $gains[$mob][exp];
                #math gains[final][cnt] $gains[final][cnt] + $gains[$mob][cnt] 
        };
        #showme {----------------------------------------------------------};
        
        #math tmp {$gains[final][exp] / $gains[final][cnt]};
        
        #echo {  %+14G %+14G %+5G}
                {$tmp}          
                {$gains[final][exp]}
                {$gains[final][cnt]}
}

#action {^The perfect match!$} {#line gag;info The perfect match! [%l]}